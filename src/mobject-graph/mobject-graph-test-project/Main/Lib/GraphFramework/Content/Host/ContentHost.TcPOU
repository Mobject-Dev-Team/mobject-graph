<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="ContentHost" Id="{29147696-d640-4aaf-b7eb-e1161d0ef104}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK ContentHost IMPLEMENTS I_Contents, I_Serializable
VAR
	hostParent : I_HostContent;
	eventTarget : I_EventTarget;
	contents : Dictionary;
	selfCreatedContent : Stack;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Constructor" Id="{173c4e6f-13af-4c18-80d2-e7a43982d081}" />
    <Folder Name="Destructor" Id="{4330abc6-1d93-4324-be08-1d57d5cfaf7b}" />
    <Folder Name="Private" Id="{5a04dfab-f06e-4a76-84a9-faa90e87eee2}" />
    <Folder Name="Protected" Id="{507c695e-d5e9-4330-b128-2f74bba9e8da}" />
    <Method Name="Accept" Id="{bb5474ac-7ae3-4961-880b-1f80e7bfa58f}">
      <Declaration><![CDATA[METHOD PUBLIC Accept
VAR_INPUT
	visitor : I_ContentVisitor;
END_VAR
VAR
	enumerator : I_ForwardEnumerator;
	content : I_Content;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[enumerator := contents.GetEnumerator();

WHILE (enumerator.MoveNext()) DO
	
	IF enumerator.TryGet(content) THEN
		content.Accept(Visitor);
	END_IF

END_WHILE

enumerator.Dispose();]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddContent" Id="{df4c12eb-d470-40f8-95fb-2344d9345235}">
      <Declaration><![CDATA[METHOD PUBLIC AddContent
VAR_INPUT
	Content : I_Content;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[contents.AddOrUpdate(Content.Name, Content);
EmitOnContentAddedEvent(Content);]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddDataAsContent" Id="{aab9615c-51fc-4b12-adec-006c1ee16fb8}">
      <Declaration><![CDATA[METHOD PUBLIC AddDataAsContent : I_Content
VAR_INPUT
	Name : T_MAXSTRING;
	Data : I_Datatype;
END_VAR
VAR
	content : I_Content;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[content := CreateContent(Name, Data);
selfCreatedContent.Push(content);
AddDataAsContent := content;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CreateContent" Id="{9c63d9f6-b735-4c61-9870-c718782d8793}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE CreateContent : I_Content
VAR_INPUT
	ContentName : T_MAXSTRING;
	ContentData : I_Datatype;
END_VAR
VAR
	pContent : POINTER TO Content;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[pContent := __NEW(Content(Name := ContentName, Data := ContentData, Parent := hostParent));
CreateContent := pContent^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="DisposeSelfCreatedContent" Id="{3adf9728-adc1-4ac9-b870-c8d82704cc33}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE DisposeSelfCreatedContent : BOOL
VAR
	content : I_Content;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[WHILE selfCreatedContent.TryPop(content) DO
	content.Dispose();
END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="EmitOnContentAddedEvent" Id="{982e38f9-d0ec-0d68-23b9-1014864696d7}" FolderPath="Protected\">
      <Declaration><![CDATA[METHOD PROTECTED EmitOnContentAddedEvent
VAR_INPUT
	Content : I_Content;
END_VAR
VAR
	pContentAddedEvent : POINTER TO ContentAddedEvent;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eventTarget = 0 THEN
	RETURN;
END_IF

pContentAddedEvent := __NEW(ContentAddedEvent(Target := Content));
eventTarget.Emit('OnContentAdded',pContentAddedEvent^);
pContentAddedEvent^.Dispose();]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{777de4d2-a95a-4a32-822e-8595232ea7b1}" FolderPath="Destructor\">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF bInCopyCode THEN
	RETURN;
END_IF

DisposeSelfCreatedContent();]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{2a740d82-de22-4725-911f-2def6b984edf}" FolderPath="Constructor\">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	Parent : I_HostContent;
	ParentEventTarget : I_EventTarget;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[hostParent := Parent;
eventTarget := parentEventTarget;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SerializeWith" Id="{a25d083a-f7ed-4bb9-acc7-2c878e04a202}">
      <Declaration><![CDATA[METHOD PUBLIC SerializeWith
VAR_INPUT
	Serializer : I_Serializer;
END_VAR
VAR
	enumerator : I_ForwardEnumerator;
	content : I_Content;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF contents.Count = 0 THEN
	RETURN;
END_IF

Serializer.AddKey('contents');
Serializer.StartArray();

enumerator := contents.GetEnumerator();

WHILE enumerator.MoveNext() DO
	IF enumerator.TryGet(content) THEN
		content.SerializeWith(Serializer);
	END_IF
END_WHILE

enumerator.Dispose();
Serializer.EndArray();]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryGetContentByName" Id="{fc9065ba-fc69-457c-8ff1-de7e3c5eded2}">
      <Declaration><![CDATA[METHOD PUBLIC TryGetContentByName : BOOL
VAR_INPUT
	Name : T_MAXSTRING;
	Destination : REFERENCE TO I_Content;
END_VAR
VAR
	content : I_Content;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT contents.TryGetValue(Name, content) THEN
	RETURN;
END_IF

Destination := content;
TryGetContentByName := TRUE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>